
plugins {
	id 'java'
	id 'maven'
	id "com.github.bjornvester.xjc" version "1.3"
}

group = "com.ixopay"
version = "2.2.1"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
}

def xjcVersion_ = "2.3.1"

sourceSets {
	example {
		java {
			compileClasspath += main.output
			runtimeClasspath += main.output
		}
	}
}

configurations {
	exampleCompile.extendsFrom compile
	exampleRuntime.extendsFrom runtime
}

dependencies {
	xjcPlugins("org.jvnet.jaxb2_commons:jaxb2-basics:0.12.0")
	xjcPlugins("org.jvnet.jaxb2_commons:jaxb2-basics-annotate:1.1.0")
	xjcPlugins("org.jvnet.jaxb2_commons:jaxb2-value-constructor:3.0")

	compile("javax.xml.bind:jaxb-api:$xjcVersion_")
}

xjc {
	xsdDir = file("$projectDir/src/main/resources/com/ixopay/client/schemas")
	xjcVersion = xjcVersion_
	generateEpisode = true
	markGenerated = false
	bindingFiles = files("$projectDir/src/main/resources/com/ixopay/client/bindings/java-ixopay.xjb")
	options =  ["-Xannotate", "-Xinheritance", "-Xvalue-constructor"]
}

javadoc {
	options.addStringOption('Xdoclint:none', '-quiet')
}

task sourcesJar(type: Jar) {
	archiveClassifier.set('sources')
	from(sourceSets.main.allSource)
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	archiveClassifier.set('javadoc')
	from(javadoc.destinationDir)
}

artifacts {
	archives sourcesJar
	archives javadocJar
}
