buildscript {
	repositories {
		jcenter()
		mavenCentral()
	}

	dependencies {
		classpath "com.github.jacobono:gradle-jaxb-plugin:1.3.6"
	}
}

apply plugin: "java"
apply plugin: "maven"
apply plugin: "com.github.jacobono.jaxb"

group = "com.ixopay"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
	// maven { url "https://jitpack.io" } only needed for unused xjc plugin
}

def xjcVersion = "2.2.7"

sourceSets {
	example {
		java {
			compileClasspath += main.output
			runtimeClasspath += main.output
		}
	}
}

dependencies {
	jaxb("org.jvnet.jaxb2_commons:jaxb2-basics-ant:0.6.5")
	jaxb("org.jvnet.jaxb2_commons:jaxb2-basics:0.6.4")
	jaxb("org.jvnet.jaxb2_commons:jaxb2-basics-annotate:0.6.4")

	jaxb("com.sun.xml.bind:jaxb-xjc:$xjcVersion")
	jaxb("com.sun.xml.bind:jaxb-impl:$xjcVersion")
	jaxb("javax.xml.bind:jaxb-api:$xjcVersion")

	jaxb("org.jvnet.jaxb2_commons:jaxb2-value-constructor:3.0")

	compile("javax.xml.bind:jaxb-api:$xjcVersion")
}

jaxb {
	System.setProperty('javax.xml.accessExternalSchema', 'all')

	xsdDir = "src/main/resources/schemas/"
	episodesDir = "src/main/resources/episodes"
	bindingsDir = "src/main/resources/bindings"
	bindings = ["ixopay.xjb"]

	xjc {
		taskClassname = "org.jvnet.jaxb2_commons.xjc.XJC2Task"
		args = ["-Xannotate", "-Xinheritance", "-Xvalue-constructor"]
	}
}

configurations {
	exampleCompile.extendsFrom compile
	exampleRuntime.extendsFrom runtime
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives sourcesJar
}
